buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE")
    }
}

apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'wcs-rest-service'
    version =  '0.1.0'
}

war {
    baseName = 'wcs-rest-service'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    testIntegration {
        java {
            srcDir 'src/test-integration/java'
        }
        resources {
            srcDir 'src/test-integration/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("com.amazonaws:aws-java-sdk-dynamodb:1.10.0")
    compile("org.apache.httpcomponents:httpclient:4.5")
    compile("org.codehaus.jackson:jackson-mapper-asl:1.9.13")
    compile("com.google.guava:guava:18.0")

    testCompile("junit:junit:4.11+") {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
    testCompile("org.hamcrest:hamcrest-library:1.3+")
    testCompile("org.mockito:mockito-core:1.9.5") {
        exclude module : 'hamcrest-core'
    }
    testCompile("info.cukes:cucumber-java:1.2.2")

    testIntegrationCompile("info.cukes:cucumber-java:1.2.2")

    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

configurations {
    cucumberRuntime {
        extendsFrom testIntegrationRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestIntegrationJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.testIntegration.output
            args = ['--plugin', 'pretty', '--glue', 'ban.steps', 'src/test-integration/resources']
        }
    }
}

idea {
    module {
        testSourceDirs += file('src/test-integration/java')
        testSourceDirs += file('src/test-integration/resources')

    }
}


